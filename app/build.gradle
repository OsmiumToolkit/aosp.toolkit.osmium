apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

//def releaseTime = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+8"))
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "aosp.toolkit.perseus"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode gitCommits
        versionName "1.4.$gitCommits"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //buildConfigField("String", "releaseTime", "$releaseTime")
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/download',
                            'src/main/res/layouts/miui',
                            'src/main/res/layouts/main',
                            'src/main/res/layouts/usage',
                            'src/main/res/layouts/welcome',
                            'src/main/res/layouts/fragment',
                            'src/main/res/layouts/script',
                            'src/main/res/layouts/disableapp',
                            'src/main/res/layouts/zxing',
                            'src/main/res/layouts/view',
                            'src/main/res'
                    ]
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
    repositories {
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    def libsuVersion = '2.3.3'
    def zxingVersion = '3.3.3'
    def supportVersion = '28.0.0'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"
    implementation 'com.squareup.okhttp3:okhttp:3.14.0'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation "com.google.zxing:core:${zxingVersion}"
    implementation "com.google.zxing:javase:${zxingVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:${supportVersion}"
    implementation "com.android.support:support-v4:${supportVersion}"
    implementation "com.android.support:design:${supportVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
